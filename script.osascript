#!/usr/bin/osascript

-- run using `osascript ./{file_name}`
-- Don't try to use the automatically detected script run thing or it'll ask for 
--  elevated permissions, which this script doesn't need

(*
  restartApplication: a function to restart an application.
  Has delays built in to accomodate for the time it takes
    to stop and restart an application
  Would be nice if there was an async/await style of action in osascript
*)
on restartApplication(applicationName)
  -- if the application is running, stop it
  if application applicationName is running then
    tell application applicationName
      quit
    end
    repeat until application applicationName is not running
      delay 0.5
    end repeat
  end if
  -- once we know the app isn't running, start it
  if application applicationName is not running then
    tell application applicationName
      activate
      tell application "System Events"
        set visible of process "Spotify" to false
      end tell
    end tell
    repeat until application applicationName is running
      delay 0.5
    end repeat
    delay 1.5
  end if
end restartApplication

-- main body of this script.
--  Make sure Spotify is running, and restart if the current track is an advertisement
if application "Spotify" is not running then
  restartApplication("Spotify")
end if

-- figure out if we're in an advertisement
tell application "Spotify"
  if player state is paused then
    play
  end if
  if player state is playing or player state is paused then
    set track_name to name of current track
    set artist_name to artist of current track
    set track_duration to duration of current track
    set player_position to player position
  end if
end tell

if track_name = "Advertisement" and artist_name = "" then
  log "Advertisement seen; restarting"
  restartApplication("Spotify")
  tell application "Spotify"
    play
    set track_name to name of current track
    set artist_name to artist of current track
    set track_duration to duration of current track
    set player_position to player position
  end tell
end if
-- program sleep until song is done
set sleep_duration to (track_duration/1000 - player_position) + 0.1

return sleep_duration
